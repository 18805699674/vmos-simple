import org.json.JSONObject

plugins {
    id 'com.android.application'
    id 'vmos-build'
}

def signingConfigValue = rootProject.ext.has("signingConfig") ? rootProject.ext.signingConfig : null
def localProject = rootProject.ext.has("localProject") ? rootProject.ext.localProject : false
android {
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdk rootProject.ext.minSdkVersion
        targetSdk  getProject().hasProperty("sampleTargetSdkVersion")?rootProject.ext.sampleTargetSdkVersion:rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        buildConfigField "String", "SERVER_PACKAGE_NAME", "\"${rootProject.ext.applicationId}\""
    }


    if (signingConfigValue) {
        signingConfigs {
            release {
                keyAlias rootProject.ext.signingConfig.keyAlias
                keyPassword rootProject.ext.signingConfig.keyPassword
                storeFile rootProject.ext.signingConfig.storeFile
                storePassword rootProject.ext.signingConfig.storePassword
            }
        }
    }

    buildTypes {
        debug {
            if (signingConfigValue) signingConfig signingConfigs.release
        }
        release {
            if (signingConfigValue) signingConfig signingConfigs.release
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }

    flavorDimensions "abi"
    productFlavors {

        universal {
            isDefault true
            dimension 'abi'
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a"
            }
        }

        arm32 {
            applicationIdSuffix ".arm32"
            dimension 'abi'
            ndk {
                abiFilters "armeabi-v7a"
            }
        }

    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation "androidx.startup:startup-runtime:1.1.1"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.vmos:sample-kit:1.1.1'
    // 此依赖是示例程序的统计数据上报 仅用于本示例程序 如果不需要请注释此依赖
    implementation 'com.vmos:vmos-lite-sample:1.0.7'
    implementation "com.github.permissions-dispatcher:permissionsdispatcher:4.9.2"
    annotationProcessor "com.github.permissions-dispatcher:permissionsdispatcher-processor:4.9.2"
    implementation 'com.github.bumptech.glide:glide:4.13.2'
    implementation 'com.tencent:mmkv:1.2.15'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'com.github.princekin-f:EasyFloat:2.0.4'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    //异常捕获库的引入
    implementation 'com.vmos:xcrash-android-lib:3.1.1'
//    implementation 'com.vmos:vmos-trace:1.0.0'
    //文件压缩/解压
    implementation 'org.zeroturnaround:zt-zip:1.16'

    implementation("com.squareup.okhttp3:okhttp:4.10.0")


    //vmos lite
    if (localProject) {
        implementation project(path: ':vmos-lite-sdk')
        compileOnly project(path: ':hiddenapi-stub')
        debugImplementation project(path: ':vmos-lite-shell')
    } else {
        implementation ("com.vmos:vmos-lite-sdk:${rootProject.ext.versionName}")/*{
            // google play上架时会对 vpn代理有限制，如果不需要运行vpn相关软件，可以去掉这个模块
            exclude group: 'com.vmos', module: 'vmos-lite-extension-vpn'
        }*/
        compileOnly 'com.vmos:hiddenapi-stub:1.1.6'
        debugImplementation'com.vmos:vmos-lite-shell:1.0.2'
    }

}

// 是否加入预置apk
def includePresetApps = true
android.applicationVariants.all { variant ->
    def buildTypeName = variant.buildType.name
    if (!includePresetApps) {
        variant.mergeAssets.doLast {
            def presetAppsDir = new File(variant.mergeAssets.outputDir.get().getAsFile(), "preset_apps")
            delete(fileTree(dir: presetAppsDir, includes: ['*.apk']))
        }
    }
    def versionCode = android.defaultConfig.versionCode
    variant.outputs.all {
        outputFileName = "${rootProject.name}-${project.name}_${buildType.name}_${flavorName}_${versionName}_${versionCode}.apk"

        processManifestProvider.get().doLast { pmp ->
            if (localProject && 'release' == buildTypeName) {
                throw new GradleException("为了保证与线上一致，release版本需要用远程依赖的版本")
            }
        }
    }
}

def buildToolsDir = new File(android.sdkDirectory.path, "build-tools")
def aaptFilepath = new File(buildToolsDir.listFiles().last(), "aapt").absolutePath

// 提取预装apk信息放到assets
JSONObject jsonObject = new JSONObject()
def assetsDir = new File(project.projectDir, "src/main/assets")
if (assetsDir.exists()) {
    // 挑出文件夹和apk
    new File(assetsDir, "preset_apps").listFiles().findAll { it.isDirectory() || it.name.endsWith('.apk') }
            .each { file ->
                // 如果是分包的 排除config和split开头的 文件最大的就是主包
                def mainApkFile = file.isDirectory() ? file.listFiles().findAll { !it.name.startsWith('config') && !it.name.startsWith('split') }
                        .max { a, b -> a.length() <=> b.length() } : file
                def proc = "${aaptFilepath} dump badging ${mainApkFile.absolutePath}".execute()
                BufferedReader br = new BufferedReader(new InputStreamReader(proc.in))
                def firstLine = br.readLine()
                br.close()
                if (firstLine != null) {
                    String[] args = firstLine.split("' ")
                    def packageName = args[0].split("='")[1]
                    def versionCode = args[1].split("='")[1].trim()
                    def versionName = args[2].split("='")[1].trim()

                    // 添加到json
                    JSONObject itemJson = new JSONObject()
                    def relativePath = file.absolutePath.replace(assetsDir.absolutePath, "").replace(File.separator, '/')
                    def assetName = relativePath.startsWith('/') ? relativePath.substring(1) : relativePath
                    itemJson.putOpt("version_code", Long.parseLong(versionCode))
                    itemJson.putOpt("version_name", versionName)
                    itemJson.putOpt("asset_path", assetName)
                    jsonObject.putOpt(packageName, itemJson)
                }
            }
    def versionFile = new File(assetsDir, 'preset_app_versions.json')
    versionFile.write(jsonObject.toString())
    println("write asset apk versions -> ${versionFile.absolutePath}\n${jsonObject.toString(4)}")
}